group 'cc.vileda.kalfor'
version '4.0.0'

buildscript {
    ext.kotlinVersion = '1.1.2-3'
    ext.extraConfVersion = '2.2.+'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:$extraConfVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

allprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.junit.platform.gradle.plugin'

    repositories {
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
        maven { url "https://dl.bintray.com/kotlin/ktor" }
        mavenCentral()
        jcenter()
    }

    ext {
        kotlinVersion = '1.1.2-3'
        appVersion = '4.0.0'
        appGroup = 'cc.vileda.kalfor'


        junitVersion = '4.12'
    }
}

subprojects {
    group 'cc.vileda.kalfor'
    version '4.0.0'

    def pomOnlyProject = project.name == "kalfor"

    apply plugin: 'optional-base'
    apply plugin: 'provided-base'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'jacoco'
    apply plugin: 'org.junit.platform.gradle.plugin'

    test.useJUnit()

    compileJava {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

        testCompile "junit:junit:${junitVersion}"
        testCompile "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
        testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M4")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M4")
    }

    if (pomOnlyProject) {
        artifacts {}
    } else {
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        task sourcesJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        artifacts {
            archives jar
            archives javadocJar
            archives sourcesJar
        }
        signing {
            required { gradle.taskGraph.hasTask(tasks.uploadArchives) }
            sign configurations.archives
        }

        task releaseSub {
            dependsOn signArchives
        }
    }

    if(project.hasProperty('sonatypeUsername')) {
        uploadArchives {

            if (!pomOnlyProject) {
                dependsOn releaseSub
            }

            repositories.mavenDeployer {
                if (!pomOnlyProject) {
                    configuration = configurations.archives
                }

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }


                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: project.getProperty('sonatypeUsername').toString(), password: project.getProperty('sonatypePassword').toString())
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: project.getProperty('sonatypeUsername').toString(), password: project.getProperty('sonatypePassword').toString())
                }

                pom.whenConfigured { configgedPom ->
                    configgedPom.dependencies.removeAll { dep ->
                        dep.scope == "test"
                    }

                    configgedPom.dependencies = configgedPom.dependencies.sort { dep ->
                        "$dep.scope:$dep.groupId:$dep.artifactId"
                    }

                    configgedPom.project {
                        name project.name
                        packaging(pomOnlyProject ? 'pom' : 'jar')
                        description project.description
                        url 'https://github.com/vileda/kalfor'
                        inceptionYear '2016'

                        scm {
                            url 'https://github.com/vileda/kalfor'
                            connection 'scm:git:git@github.com:vileda/kalfor.git'
                        }

                        licenses {
                            license {
                                name 'Apache-2.0 License'
                                url 'http://www.apache.org/licenses/LICENSE-2.0'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'vileda'
                                name 'Tristan Leo'
                                email 'vileda+kalfor@vileda.cc'
                                url 'https://github.com/vileda/kalfor'
                            }
                        }

                        issueManagement {
                            system = "GitHub"
                            url = "https://github.com/vileda/kalfor/issues"
                        }
                    }
                }

            }
        }
    } else {
        uploadArchives { repositories { mavenDeployer {} } }
    }

    junitPlatform {
        // platformVersion '1.0.0-M4'
        filters {
            engines {
                // include 'junit-jupiter', 'junit-vintage'
                // exclude 'custom-engine'
            }
            tags {
                // include 'fast'
                exclude 'slow'
            }
            // includeClassNamePattern '.*Test'
        }
        // enableStandardTestTask true
        // reportsDir file('build/test-results/junit-platform') // this is the default
        logManager 'org.apache.logging.log4j.jul.LogManager'
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

gradle.taskGraph.whenReady { taskGraph ->
    allprojects*.version = appVersion
    allprojects*.group = appGroup
}

task jars() {
    dependsOn subprojects.findAll { it.name != 'kalfor' }.jar
    dependsOn subprojects.findAll { it.name != 'kalfor' }.sourcesJar
    dependsOn subprojects.findAll { it.name != 'kalfor' }.javadocJar
}

task release() {
    dependsOn subprojects.uploadArchives
}

defaultTasks 'clean', 'jars'
